package rules.Ava;

import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.util.Agents;
import com.mindsmiths.ruleEngine.model.Initialize;

import com.mindsmiths.armory.events.UserConnectedEvent;
import com.mindsmiths.armory.events.SubmitEvent;

import com.mindsmiths.sdk.utils.templating.*;

import com.mindsmiths.employeeManager.employees.Employee;

import java.util.List;
import java.util.Map;
import java.util.HashMap;

import signals.AllEmployees;
import signals.EmployeeUpdateSignal;

import agents.Ava;
import agents.CultureMaster;
import models.EmployeeProfile;
import models.OnboardingStage;
import models.AvaLunchCycleStage;
import models.AvaiablePersonalQuestions;

rule "Send welcome email"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: Ava(
            workingHours == true,
            onboardingStage == null
        )
        employee: EmployeeProfile()
    then
        agent.sendWelcomeEmail(employee);
        modify(agent) {
            setOnboardingStage(OnboardingStage.EMAIL_SENT)
        };
end

rule "Start and reconnect familiarity quiz"
   when
        signal: UserConnectedEvent(getParamAsString("trigger") == "start-onboarding") from entry-point "signals"
        agent: Ava(onboardingStage in (OnboardingStage.EMAIL_SENT, OnboardingStage.FAMILIARITY_QUIZ))
   then
        agent.showFamiliarityQuizScreens();
        modify(agent) {
            setOnboardingStage(OnboardingStage.FAMILIARITY_QUIZ)
        };
        delete(signal);
end

rule "Start personal quiz intro"
   when
        signal: SubmitEvent(getParamAsString("submit") == "finished-familiarity-quiz") from entry-point "signals"
        agent: Ava(onboardingStage == OnboardingStage.FAMILIARITY_QUIZ)
        employee: EmployeeProfile()
   then 
        Map<String, Double> familiarity = new HashMap<String, Double>(
            employee.getFamiliarity()
        );
        for (String paramId : signal.getParams().keySet()){
            if (paramId.startsWith("answers")) {
                for (String em : (List<String>) signal.getParam(paramId)) {
                    familiarity.put(
                        em, familiarity.getOrDefault(em, 0.0) + 1.0
                    );
                }
            }
        }

        modify(employee) { 
            setFamiliarity(familiarity)
        };
        agent.showPersonalQuizIntroScreens();
        modify(agent) {
            chargeConnectionNeurons(employee),
            setOnboardingStage(OnboardingStage.PERSONAL_QUIZ_INTRO)
        };
        delete(signal);
end

rule "Reconnect personal quiz intro"
    when
        signal: UserConnectedEvent(getParamAsString("trigger") == "start-onboarding") from entry-point "signals"
        agent: Ava(onboardingStage == OnboardingStage.PERSONAL_QUIZ_INTRO)
    then
        agent.showPersonalQuizIntroScreens();
        delete(signal);
end

rule "Setup Ava for personal quiz"
    when
        signal: SubmitEvent(getParamAsString("submit") == "startPersonalQuiz") from entry-point "signals"
        agent: Ava(onboardingStage == OnboardingStage.PERSONAL_QUIZ_INTRO)
        questions: AvaiablePersonalQuestions()
    then
        modify(agent) {setOnboardingStage(OnboardingStage.PERSONAL_QUIZ)};
        agent.showPersonalQuizScreens(questions.getCurrentQuestion());
        delete(signal);
end

rule "Show personal quiz questions"
    when
        agent: Ava(onboardingStage == OnboardingStage.PERSONAL_QUIZ)
        EmployeeProfile(personalAnswers.size() < 3)
        questions: AvaiablePersonalQuestions()
    then
        agent.showPersonalQuizScreens(questions.getCurrentQuestion());
        modify(agent) {setOnboardingStage(OnboardingStage.WAIT_FOR_PERSONAL_ANSWER)};
end

rule "Store personal quiz answers"
    when
        agent: Ava(onboardingStage == OnboardingStage.WAIT_FOR_PERSONAL_ANSWER)
        signal: SubmitEvent(getParamAsString("submit") == "submit") from entry-point "signals"
        employee: EmployeeProfile(getPersonalAnswers().size() < 3)
        questions: AvaiablePersonalQuestions()
    then
        Map<String, String> personalAnswers = new HashMap<String, String>(
            employee.getPersonalAnswers()
        );
        for (String paramId : signal.getParams().keySet())
            if (paramId.startsWith("answers"))
                personalAnswers.put(
                    paramId.replace("answers", "question"),
                    signal.getParamAsString(paramId)
                );

        modify(employee) {setPersonalAnswers(personalAnswers)};
        modify(questions) {popAnsweredQuestion()};
        modify(agent) {setOnboardingStage(OnboardingStage.PERSONAL_QUIZ)};
        delete(signal);
end

rule "Skip personal quiz question"
    when
        agent: Ava(onboardingStage == OnboardingStage.WAIT_FOR_PERSONAL_ANSWER)
        signal: SubmitEvent(getParamAsString("skip") == "skip") from entry-point "signals"
        EmployeeProfile(getPersonalAnswers().size() < 3)
        questions: AvaiablePersonalQuestions()
    then
        modify(questions) {nextQuestion()};
        modify(agent) {setOnboardingStage(OnboardingStage.PERSONAL_QUIZ)};
        delete(signal);
end

rule "Finish onboarding"
   when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: Ava(onboardingStage == OnboardingStage.PERSONAL_QUIZ)
        employee: EmployeeProfile(getPersonalAnswers().size() == 3)
   then
        modify(employee) {
            setOnboardingStage(OnboardingStage.STATS_EMAIL) 
        };
        
        modify(agent) {
            setOnboardingStage(OnboardingStage.STATS_EMAIL),
            setLunchCycleStage(AvaLunchCycleStage.LUNCH_MAIL_SENDING),
            setStatsEmailLastSentAt(ts)
        };
        agent.showPersonalQuizOutroScreens();
        agent.send(CultureMaster.ID, new EmployeeUpdateSignal(employee));
end

rule "Reconnect final screen"
   when
        signal: UserConnectedEvent(getParamAsString("trigger") == "start-onboarding") from entry-point "signals"
        agent: Ava(onboardingStage in (OnboardingStage.STATS_EMAIL, OnboardingStage.FINISHED))
   then
        agent.showFinalScreen();
        delete(signal);
end

rule "Send statistics email"
    when
        agent: Ava(
            onboardingStage == OnboardingStage.STATS_EMAIL,
            allEmployeesFinishedOnboarding() == true
        )
        employee: EmployeeProfile()
    then
        agent.sendStatisticsEmail(employee);
        modify(agent) {setOnboardingStage(OnboardingStage.FINISHED)};
end