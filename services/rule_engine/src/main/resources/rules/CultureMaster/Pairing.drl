package rules.CultureMaster;

import java.util.Date;

import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.model.Heartbeat;
import com.mindsmiths.ruleEngine.util.Agents;
import com.mindsmiths.ruleEngine.model.Initialize;
import com.mindsmiths.pairingalgorithm.Matches;

import signals.ConnectionsStrengthSignal;
import signals.EmployeeAvailabilitySignal;

import agents.CultureMaster;
import models.CmLunchCycleStage;


rule "First contact"
    salience 100
    when
        initialize: Initialize() from entry-point "agent-created"
        agent: CultureMaster()
    then
        Log.info("Created new CultureMaster agent.");
        delete(initialize);
end

rule "Store Ava availability"
    when
        signal: EmployeeAvailabilitySignal() from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)
    then
        modify(agent) {addEmployeeAvailability(signal.getEmployeeAvailability())};
        Log.info("Storing availability: " + agent.getEmployeeAvailabilities());
        delete(signal);
end

rule "Store Ava connections strengths"
    when
        signal: ConnectionsStrengthSignal() from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {getEmployeeConnectionStrengths().put(signal.getEmployeeId(), signal.getConnectionStrengths())};   
        Log.info("Storing connection strength: " + agent.getEmployeeConnectionStrengths());
        delete(signal);
end

rule "Check that all ava availabilities came"
    when
        Heartbeat() from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES,
                            employees.size() == employeeAvailabilities.size(),
                            !employees.isEmpty())
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.GENERATE_MATCHES)};
end

rule "Generate this weeks matches"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.GENERATE_MATCHES)
    then
        Log.info("Generating matches....");
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_GENERATED_MATCHES)};
        agent.generateMatches();
end

rule "Store generated matches"
    when
        matches: Matches() from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_GENERATED_MATCHES)
    then
        modify(agent) {addMatches(matches.getAllMatches())};
        Log.info("Matches from pairing algorithm: " + matches.getAllMatches());
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.SEND_TO_AVAS)};
        delete(matches);
end

rule "Send match info to Avas"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.SEND_TO_AVAS)
    then
        agent.sendMatches();
        agent.clearEmployeeAvailabilities();
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)};
end