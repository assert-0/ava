package rules.CultureMaster;

import java.util.Date;

import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.model.Heartbeat;
import com.mindsmiths.ruleEngine.util.Agents;
import com.mindsmiths.ruleEngine.model.Initialize;
import com.mindsmiths.pairingalgorithm.Matches;

import signals.AvaRegisteredSignal;
import signals.RequestAvailableDaysSignal;
import signals.AvaAvailabilitySignal;

import agents.CultureMaster;
import models.CmLunchCycleStage;


rule "First contact"
    salience 100
    when
        initialize: Initialize() from entry-point "agent-created"
        agent: CultureMaster()
    then
        Log.info("Created new CultureMaster agent.");
        delete(initialize);
end

rule "Add New Ava Id"
    when
        signal: AvaRegisteredSignal() from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {addNewAva(signal.getAvaId())};
        delete(signal);
end

rule "Store Ava Availability"
    when
        signal: AvaAvailabilitySignal() from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)
    then
        Log.info("Received some ava availability\n");
        modify(agent) {addAvaAvailability(signal.getAvaAvailability())};
        Log.info(agent.getAvaAvailabilities());
        delete(signal);
end


rule "Check That All Ava Availabilities Came"
    when
        Heartbeat() from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES,
                            avaIDs.size() == avaAvailabilities.size(),
                            !avaIDs.isEmpty())
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.GENERATE_MATCHES)};
end


rule "Generate This Weeks Matches"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.GENERATE_MATCHES)
    then
        Log.info("Generating matches....");
        Log.info("AvaIDs List: " + agent.getAvaIDs());
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_GENERATED_MATCHES)};
        agent.generateMatches();
end

rule "Store Generated Matches"
    when
        matches: Matches() from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {addMatches(matches.getAllMatches())};
        Log.info("---------------------rezultati pairinga---------------------");
        Log.info(matches.getAllMatches());
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.SEND_TO_AVAS)};
        delete(matches);
end

rule "Send Match Info To Avas"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.SEND_TO_AVAS)
    then
        agent.sendMatches();
end


