package rules.cultureMaster;

import agents.CultureMaster;
import signals.EmployeeUpdateSignal;
import com.mindsmiths.employeeManager.ManualTriggerEvent;
import com.mindsmiths.ruleEngine.util.Log;
import com.mindsmiths.ruleEngine.model.Heartbeat;
import models.CmLunchCycleStage;
import static com.mindsmiths.ruleEngine.util.DateUtil.evaluateCronExpression;

rule "Add or update Ava data"
    when
        signal: EmployeeUpdateSignal() from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {
            addOrUpdateEmployee(signal),
            sendEmployeesToAva()
        };
        delete(signal);
end

rule "Trigger lunch cycle for CultureMaster"
    when
        signal: ManualTriggerEvent(triggerType == "lunch_cycle") from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES)};
        delete(signal);
end

rule "Trigger lunch pairing for CultureMaster"
    when
        signal: ManualTriggerEvent(triggerType == "lunch_pairing") from entry-point "signals"
        agent: CultureMaster()
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_GENERATED_MATCHES)};
        agent.generateMatches();
        delete(signal);
end

rule "Trigger familiarity quiz"
    when
        signal: ManualTriggerEvent(triggerType == "familiarity_quiz") from entry-point "signals"
        agent: CultureMaster()
    then
        Log.info("CultureMaster: triggered familiarity quiz from dashboard");
        // trigger quiz
end

rule "Lunch pairing"
    when
        Heartbeat(ts: timestamp) from entry-point "signals"
        eval(evaluateCronExpression("* * 12-13 ? * THU *", ts, "Europe/Zagreb"))
        agent: CultureMaster(lunchCycleStage == CmLunchCycleStage.COLLECT_AVA_AVAILABILITIES 
            || lunchCycleStage == null)
    then
        modify(agent) {setLunchCycleStage(CmLunchCycleStage.COLLECT_GENERATED_MATCHES)};
        agent.generateMatches();
end